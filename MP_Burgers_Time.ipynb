{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "abffe742-2615-4381-bb68-73c579334338",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "run_ref (generic function with 1 method)"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Statistics\n",
    "using LinearAlgebra\n",
    "using FFTW\n",
    "using Dates\n",
    "using TimerOutputs\n",
    "using DelimitedFiles # For CSV output\n",
    "using Printf        # For formatted printing\n",
    "using XLSX          # For writing to Excel files\n",
    "using DataFrames\n",
    "\n",
    "\n",
    "# -----------------------------\n",
    "# Precision setup\n",
    "# -----------------------------\n",
    "const FullFloat = Float64\n",
    "const ReduFloat = Float32\n",
    "\n",
    "# -----------------------------\n",
    "# Build spatial grid and derivative matrix\n",
    "# -----------------------------\n",
    "function setup_problem(Nx::Int, L::FullFloat, FullFloatType::DataType, ReduFloatType::DataType)\n",
    "    x = LinRange(FullFloat(0), L, Nx + 1)[1:end-1]\n",
    "    dx = x[2] - x[1]\n",
    "\n",
    "    # Define k_vals in un-shifted order\n",
    "    k_vals = collect(FullFloatType, vcat(0:((Nx-1) รท 2), -((Nx-1) รท 2):-1))\n",
    "\n",
    "    # Full precision derivative matrix\n",
    "    Iden_high = Matrix{FullFloatType}(I, Nx, Nx)\n",
    "    K_high = Diagonal(k_vals)\n",
    "    Dx_high = real(ifft((im * one(FullFloatType)) * K_high * fft(Iden_high, 1), 1))\n",
    "\n",
    "    # Low precision derivative matrix\n",
    "    Iden_low = Matrix{ReduFloatType}(I, Nx, Nx)\n",
    "    K_low = Diagonal(ReduFloatType.(k_vals))\n",
    "    Dx_low = real(ifft((im * one(ReduFloatType)) * K_low * fft(Iden_low, 1), 1))\n",
    "    Dx_low = ReduFloatType.(Dx_low) \n",
    "    \n",
    "    return x, dx, Dx_high, Dx_low\n",
    "end\n",
    "\n",
    "\n",
    "# -----------------------------\n",
    "# Newton solver for one step (low precision)\n",
    "# -----------------------------\n",
    "function newton_solve(f::Function, x0::Vector{ReduFloat}, Dx_low::Matrix{ReduFloat}, dt_low::ReduFloat; maxiter::Int = 100)\n",
    "    tol = 1e-5\n",
    "    x = copy(x0)\n",
    "    Id = Matrix{ReduFloat}(I, length(x0), length(x0))\n",
    "    J = similar(Id)\n",
    "    fx = similar(x)\n",
    "    h2 = dt_low / ReduFloat(2)\n",
    "    for iter in 1:maxiter\n",
    "        fx .= f(x)\n",
    "        res = norm(fx, Inf)\n",
    "        if res < tol\n",
    "            return x, res # Return converged solution and iteration count\n",
    "        end\n",
    "        J .= Id .+ (h2) .* (Dx_low * Diagonal(x))\n",
    "        x .= x .- J \\ fx # Corrected update step\n",
    "    end\n",
    "    # Return the non-converged solution and max iterations\n",
    "    return x, norm(fx, Inf)\n",
    "end\n",
    "\n",
    "\n",
    "# -----------------------------\n",
    "# Time integration using Newton's method (Mixed Precision)\n",
    "# -----------------------------\n",
    "function run_newton_mp(x::Vector{FullFloat}, Dx_high::Matrix{FullFloat}, Dx_low::Matrix{ReduFloat}; \n",
    "                       Nt::Int, dt::FullFloat)\n",
    "    u = sin.(x)\n",
    "    ulow = ReduFloat.(u)\n",
    "    dt_low = ReduFloat(dt)\n",
    "    y_high = similar(u)\n",
    "\n",
    "    for i in 1:Nt\n",
    "        ulow .= ReduFloat.(u)\n",
    "        f_low = y -> y .- ulow .- (dt_low / ReduFloat(2)) .* (ReduFloat(-0.5) .* Dx_low * (y .^ ReduFloat(2)))\n",
    "        y_low, res = newton_solve(f_low, ulow, Dx_low, dt_low)\n",
    "        y_high .= FullFloat.(y_low)\n",
    "        u .= u .+ dt .* (FullFloat(-0.5) .* Dx_high * (y_high.^FullFloat(2)))\n",
    "        if any(isnan, u)\n",
    "            println(\"Warning: NaN detected in Mixed-Precision Newton run.\")\n",
    "            break   \n",
    "        end\n",
    "    end\n",
    "    return u\n",
    "end\n",
    "\n",
    "# -----------------------------\n",
    "# Broyden solver for one time step\n",
    "# -----------------------------\n",
    "function broyden_solve(y0::Vector{FullFloat}, un::Vector{FullFloat}, dt::FullFloat, Dx::Matrix{FullFloat}, \n",
    "                       Ji::Matrix{FullFloat}, ResTol::FullFloat; good::Bool=true, maxiter::Int=1)\n",
    "    y = copy(y0)\n",
    "    fun_curr =  y .- un .+ (dt / 4) .* (Dx * (y.^ 2))\n",
    "    # To avoid extra allocation\n",
    "    fun_old = similar(fun_curr)\n",
    "    s = similar(y)\n",
    "    b = similar(y')\n",
    "    dy = similar(y)\n",
    "    \n",
    "    \n",
    "    for ia in 1:maxiter\n",
    "        fun_old .= fun_curr\n",
    "        s .= -Ji * fun_old\n",
    "        y .= y .+ s\n",
    "        fun_curr .= y .- un .+ (dt / 4) .* (Dx * (y.^ 2))  \n",
    "        dy .= fun_curr - fun_old\n",
    "        b .= good ? (s' * Ji) : dy'\n",
    "        denom = (b * dy)\n",
    "        if abs(denom) < 1e-12\n",
    "            return y, Ji\n",
    "        end\n",
    "        Ji .= Ji + ((s - Ji * dy) * b) / (denom)\n",
    "        res = norm(fun_curr, Inf)\n",
    "        if res < ResTol \n",
    "            return y,  Ji\n",
    "        end\n",
    "    end\n",
    "    return y,  Ji\n",
    "end\n",
    "\n",
    "# -----------------------------\n",
    "# Broyden solver for semi-implicit midpoint rule\n",
    "# -----------------------------\n",
    "function run_broyden_midpoint(x::Vector{FullFloat}, Dx_high::Matrix{FullFloat}, Dx_low::Matrix{ReduFloat}; \n",
    "                              Nx::Int, Nt::Int, dt::FullFloat, ResTol::FullFloat)\n",
    "\n",
    "    dt_low = ReduFloat(dt)\n",
    "    un = sin.(x)\n",
    "    ulow = ReduFloat.(un)\n",
    "    Iden_low = Matrix{ReduFloat}(I, Nx, Nx)\n",
    "    J = Iden_low .+ (ReduFloat(0.5) * dt_low) .* (Dx_low .* ulow')\n",
    "    Ji = FullFloat.(inv(J))\n",
    "    good = true\n",
    "    f1 = similar(un)\n",
    "    y = similar(un)\n",
    "    fx = similar(un)\n",
    "\n",
    "    for i in 1:Nt\n",
    "\n",
    "        # Low precision implicit solve\n",
    "        ulow .= ReduFloat.(un)      \n",
    "        f_low = y -> y .- ulow .+ (dt_low / ReduFloat(2)) .* (ReduFloat(0.5) .* Dx_low * (y .^ ReduFloat(2)))\n",
    "        y_low, res = newton_solve(f_low, ulow, Dx_low, dt_low)\n",
    "        # Corrections\n",
    "        y .= FullFloat.(y_low)\n",
    "        #if res > ResTol\n",
    "        y, Ji = broyden_solve(y, un, dt, Dx_high, Ji, ResTol; good=good)\n",
    "        #end\n",
    "        # Explicit stage\n",
    "        f1 .= -Dx_high * (0.5 .* (y .^ FullFloat(2)))\n",
    "        un .= un .+ dt .* f1\n",
    "\n",
    "        if any(isnan, un)\n",
    "            println(\"Warning: NaN detected in Broyden run.\")\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return un\n",
    "end\n",
    "\n",
    "\n",
    "# -----------------------------\n",
    "# NEWTON SOLVER for IMR (Full Precision)\n",
    "# -----------------------------\n",
    "function run_newton_full_precision(x::Vector{FullFloat}, Dx_high::Matrix{FullFloat};\n",
    "                                   Nx::Int, Nt::Int, dt::FullFloat,\n",
    "                                   maxiter::Int=100)\n",
    "    \n",
    "    un = sin.(x)\n",
    "    Iden = Matrix{FullFloat}(I, Nx, Nx)\n",
    "    J = similar(Iden)\n",
    "    ResTol = 1e-10\n",
    "    s = similar(un)\n",
    "    f1 = similar(un)\n",
    "    y = similar(un)\n",
    "    fx = similar(un)\n",
    "\n",
    "    for i in 1:Nt\n",
    "        y .= un\n",
    "        for ia in 1:maxiter\n",
    "            fx .= y .- un .+ (dt / 4) .* (Dx_high * (y.^ 2))\n",
    "            res = norm(fx, Inf)\n",
    "            if res < ResTol\n",
    "                break\n",
    "            end\n",
    "            J .= Iden .+ (dt / 2) .* (Dx_high * Diagonal(y))\n",
    "            s .= - (J \\ fx)\n",
    "            y .= y .+ s\n",
    "        end\n",
    "        \n",
    "\n",
    "        f1 .= -Dx_high * (0.5 .* (y .^ 2))\n",
    "        un .= un .+ dt .* f1\n",
    "        \n",
    "        if any(isnan, un)\n",
    "            println(\"Warning: NaN detected in full-precision Newton.\")\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return un\n",
    "end\n",
    "\n",
    "function rk4_step(u, D, dt)\n",
    "    \n",
    "    k1 = -0.5*D*(u.^2)\n",
    "    k2 = -0.5*D*(u + 0.5 * dt * k1).^2\n",
    "    k3 = -0.5*D*(u + 0.5 * dt * k2).^2\n",
    "    k4 = -0.5*D*(u + dt * k3).^2\n",
    "    return u + (dt / 6) * (k1 + 2*k2 + 2*k3 + k4)\n",
    "end\n",
    "\n",
    "function run_ref(x::Vector{FullFloat}, Dx::Matrix{FullFloat}; Nt::Int, dt::FullFloat)\n",
    "    un = sin.(x)\n",
    "    for i in 1:Nt\n",
    "        un .= rk4_step(un, Dx, dt)\n",
    "    end\n",
    "    return un\n",
    "end\n",
    "\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "b464e191-2f33-4d79-8100-cae407bdb5da",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "run_experiment (generic function with 1 method)"
      ]
     },
     "execution_count": 11,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# -----------------------------\n",
    "# Main Experiment Function\n",
    "# -----------------------------\n",
    "function run_experiment()\n",
    "    # --- Experiment Parameters ---\n",
    "    Nx_values = [201, 301]\n",
    "    Nt_values = [7000, 14000]\n",
    "    tFinal = 0.7\n",
    "    L = 2 * pi\n",
    "    NUM_RUNS = 10\n",
    "    Nt_ref = 10000\n",
    "    FullFloat = Float64\n",
    "    ReduFloat = Float32\n",
    "\n",
    "\n",
    "    # --- DataFrame Initialization ---\n",
    "    # Added columns for the reference Newton solver results.\n",
    "    results_df = DataFrame(\n",
    "        Nx = Int[],\n",
    "        Nt = Int[],\n",
    "        dx = Float64[],\n",
    "        dt = Float64[],\n",
    "        CFL = Float64[],\n",
    "        # FP\n",
    "        Time_FP = Float64[],\n",
    "        Error_FP= Float64[],\n",
    "        Time_MP = Float64[],\n",
    "        Error_MP = Float64[],\n",
    "        Speedup_MP = Float64[],\n",
    "        # MP with corrections\n",
    "        Time_B_MP = Float64[],\n",
    "        Error_B_MP= Float64[],\n",
    "        Speedup_B_MP = Float64[],\n",
    "\n",
    "    )\n",
    "\n",
    "    println(\"--- Starting Benchmark ---\")\n",
    "    println(\"Averaging timings over $NUM_RUNS runs for each solver.\")\n",
    "    println(\"High precision (FullFloat): \", FullFloat)\n",
    "    println(\"Low precision (ReduFloat): \", ReduFloat)\n",
    "    println(\"Reference Nt for error calculation: $Nt_ref\")\n",
    "    println(\"-\"^60)\n",
    "\n",
    "    for Nx in Nx_values\n",
    "        println(\"Setting up for Nx = $Nx...\")\n",
    "        x_linrange, dx, Dx_high, Dx_low = setup_problem(Nx, L, FullFloat, ReduFloat)\n",
    "        x = collect(x_linrange)\n",
    "\n",
    "        println(\" Generating high-accuracy reference solution...\")\n",
    "        dt_ref = tFinal / Nt_ref\n",
    "        \n",
    "        # Time the reference solution generation and capture its results\n",
    "        ref_time = 0.0\n",
    "        ref_result = nothing\n",
    "        ref_time = @elapsed begin\n",
    "            u_ref = run_ref(x, Dx_high; Nt = Nt_ref, dt = dt_ref)\n",
    "        end\n",
    "        @printf \" Reference solution generated in %.4fs .\\n\" ref_time\n",
    "\n",
    "        for Nt in Nt_values\n",
    "            dt = tFinal / Nt\n",
    "            cfl = dt / dx\n",
    "            @printf \" Running case: Nx=%d, Nt=%d, CFL=%.4f.\\n\" Nx Nt cfl \n",
    "\n",
    "            u_fp = u_mp = u_mp_b = nothing\n",
    "\n",
    "            # Full precision\n",
    "            total_time_fp = fill(0.0, NUM_RUNS)\n",
    "            for i in 1:NUM_RUNS\n",
    "                total_time_fp[i] = @elapsed begin\n",
    "                     u_fp = run_newton_full_precision(x, Dx_high; Nx=Nx, Nt=Nt, dt=dt)\n",
    "                end\n",
    "            end\n",
    "            time_fp = mean(total_time_fp[2:end])\n",
    "            error_fp = norm(u_fp .- u_ref, Inf)\n",
    "\n",
    "\n",
    "            # Mixed Precision with no corrections\n",
    "            total_time_mp = fill(0.0, NUM_RUNS)\n",
    "            for i in 1:NUM_RUNS\n",
    "                total_time_mp[i] = @elapsed begin\n",
    "                    u_mp = run_newton_mp(x, Dx_high, Dx_low; Nt = Nt, dt = dt)\n",
    "                end\n",
    "            end\n",
    "            time_mp = mean(total_time_mp[2:end])\n",
    "            error_mp = norm(u_mp .- u_ref, Inf)\n",
    "\n",
    "\n",
    "            # Mixed Precision with corrections\n",
    "            total_time_mp_b = fill(0.0, NUM_RUNS)\n",
    "            for i in 1:NUM_RUNS\n",
    "                total_time_mp_b[i] = @elapsed begin\n",
    "                    u_mp_b = run_broyden_midpoint(x, Dx_high, Dx_low; \n",
    "                        Nx = Nx, Nt = Nt, dt = dt, ResTol=1e-12)\n",
    "                end\n",
    "            end\n",
    "            time_mp_b = mean(total_time_mp_b[2:end])\n",
    "            error_mp_b = norm(u_mp_b .- u_ref, Inf)\n",
    "\n",
    "            speedup_mp = time_fp / time_mp\n",
    "            speedup_mp_b = time_fp / time_mp_b\n",
    "\n",
    "            # --- Push a new row to the DataFrame with reference solver data ---\n",
    "            new_row = (\n",
    "                Nx = Nx, Nt = Nt, dx = dx, dt = dt, CFL = cfl, Time_FP =  time_fp, Error_FP = error_fp, \n",
    "                Time_MP = time_mp, Error_MP = error_mp, Speedup_MP = speedup_mp,  \n",
    "                Time_B_MP = time_mp_b, Error_B_MP = error_mp_b,  Speedup_B_MP =  speedup_mp_b\n",
    "            )\n",
    "            push!(results_df, new_row)\n",
    "        end\n",
    "        println(\"-\"^60)\n",
    "    end\n",
    "\n",
    "    println(\"\\n--- Benchmark Results Summary (from DataFrame) ---\")\n",
    "\n",
    "    # --- Generate a unique filename using a timestamp ---\n",
    "    timestamp = Dates.format(now(), \"yyyy-mm-dd_HH-MM-SS\")\n",
    "    # output_filename = \"benchmark_results_$(timestamp).xlsx\"\n",
    "\n",
    "    # Make sure this file doesn't already exist in your directory\n",
    "    output_filename = \"IMR_BRG_1e4.xlsx\"\n",
    "\n",
    "    # --- Export the DataFrame ---\n",
    "    # Now you don't need `overwrite=true` because the filename is always new.\n",
    "    XLSX.writetable(output_filename, results_df, sheetname=\"Results\")\n",
    "\n",
    "    println(\"\\nBenchmark finished.\")\n",
    "    println(\"Successfully exported results to '$(output_filename)'.\")\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "2a13f785-e612-4bf2-b0f7-b001897da829",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "--- Starting Benchmark ---\n",
      "Averaging timings over 10 runs for each solver.\n",
      "High precision (FullFloat): Float64\n",
      "Low precision (ReduFloat): Float32\n",
      "Reference Nt for error calculation: 10000\n",
      "------------------------------------------------------------\n",
      "Setting up for Nx = 201...\n",
      " Generating high-accuracy reference solution...\n",
      " Reference solution generated in 0.5325s .\n",
      " Running case: Nx=201, Nt=7000, CFL=0.0032.\n",
      " Running case: Nx=201, Nt=14000, CFL=0.0016.\n",
      "------------------------------------------------------------\n",
      "Setting up for Nx = 301...\n",
      " Generating high-accuracy reference solution...\n",
      " Reference solution generated in 0.5581s .\n",
      " Running case: Nx=301, Nt=7000, CFL=0.0048.\n",
      " Running case: Nx=301, Nt=14000, CFL=0.0024.\n",
      "------------------------------------------------------------\n",
      "\n",
      "--- Benchmark Results Summary (from DataFrame) ---\n",
      "\n",
      "Benchmark finished.\n",
      "Successfully exported results to 'IMR_BRG_1e4.xlsx'.\n"
     ]
    }
   ],
   "source": [
    "# -----------------------------\n",
    "# Run the experiment\n",
    "# -----------------------------\n",
    "run_experiment()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7ebb0f88-a9af-4e13-88a7-c8a52b408754",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.6",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
